%%
%% This implements the event block
%%
%% Copyright (C) 2008
%% Richard Hacker
%% IgH Essen GmbH
%% 
%% License: GPLv3+

%implements "event" "C"

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%function Outputs( block, system ) Output
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

  /* %<Type> Block: %<Name>
   */
  %assign RollVars = ["Y", "DWork", "P"]
  %roll sigIdx = RollRegions, lcv = RollThreshold, block, ...
      "Roller", RollVars
    %%
    %assign output = LibBlockOutputSignal(0,"",lcv,sigIdx)
    %%
    %if SingleShot
      %<output> = %<LibBlockParameter(Trigger, "", lcv, sigIdx)> !=
        %<LibBlockDWork(DWORK1, "", lcv, sigIdx)>;
      %<LibBlockDWork(DWORK1, "", lcv, sigIdx)> = \
        %<LibBlockParameter(Trigger, "", lcv, sigIdx)>;
    %else
      if (%<output>) {
        %<output> = 0;
      }
      else if (%<LibBlockParameter(Trigger, "", lcv, sigIdx)> != \
                %<LibBlockDWork(DWORK1, "", lcv, sigIdx)>) {
        %<output> = 1;
        %<LibBlockDWork(DWORK1, "", lcv, sigIdx)>++;
      }
    %endif
  %endroll
%endfunction
